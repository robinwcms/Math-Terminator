// Anchor the pause button to the view's actual top-right corner so it
// stays in the corner regardless of view zoom (e.g. sniper scope).
var _vx = camera_get_view_x(view_camera[0]);
var _vy = camera_get_view_y(view_camera[0]);
var _vw = camera_get_view_width(view_camera[0]);
if (global.is_touch)
{
	x = _vx + _vw - 70;
	y = _vy + 60;
}
else
{
	x = _vx + _vw - 50;
	y = _vy + 50;
}

// Checks if the game is playing and wasnt paused
if (obj_game_manager.curr_game_state == GAME_STATE.PLAYING && !obj_game_manager.was_paused)
{
	// Checks if the escape button has been pressed
	if (keyboard_check_pressed(vk_escape))
	{
		// Calls the pause game function
		obj_game_manager.pause_game();
		// Sets the key variable to pressed
		is_pressed = true;
		// Sets the target scale
		target_scale = 0.9;
		// Speeds up the scale rate
		scale_rate = 0.9;
	}
	else
	{
		// Variable stores gamepad count
		var _max_pads = gamepad_get_device_count();

		// Loops though the gamepads
		for (var _i = 0; _i < _max_pads; _i++)
		{
			// Checks gamepad is connected
			if (gamepad_is_connected(_i))
			{
				// Checks if the start button has been pressed	
				if (gamepad_button_check_pressed(_i, gp_start))
				{
					// Calls the pause game function
					obj_game_manager.pause_game();
					// Sets the key variable to pressed
					is_pressed = true;
					// Sets the target scale
					target_scale = 0.9;
					// Speeds up the scale rate
					scale_rate = 0.9;
				}
			}
		}
	}
}

// Checks if the image is at the target scale
if (image_xscale != target_scale || image_yscale != target_scale)
{
	// Checks if scale changes at rate or snaps to size
	if (can_scale_at_rate)
	{
		// Lerps the scale towards the target scale
		image_xscale = lerp(image_xscale, target_scale, scale_rate);
		image_yscale = lerp(image_yscale, target_scale, scale_rate);
	}
	else
	{
		// Hard sets the scale to new target scale
		image_xscale = target_scale;
		image_yscale = target_scale;
	}
}

// Checks if the pressed state is true
if (is_pressed)
{
	// Checks if the button has reached its target scale
	if (image_xscale == target_scale && image_yscale == target_scale)
	{
		// Checks if the image scale is returned to full (1)
		if (image_xscale == 1 && image_yscale == 1)
		{
			// Resets the buttons pressed state
			is_pressed = false;
			// Resets the buttons scale rate
			scale_rate = 0.1;
		}
		else
		{
			// Resets the buttons scale
			target_scale = 1.0;	
		}
	}
}